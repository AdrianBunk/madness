dnl
dnl   configure script for MADNESS package
dnl
dnl   liberally borrowed from MPQC, etc.
dnl 

dnl
dnl   run "aclocal -I lib/autoconf", then "autoconf", to produce configure script
dnl

# Definitions
define([madness_mmm_version],[1.0.0])
define([madness_buildid],[alpha])
define([madness_so_version],[1:0:0])
define([madness_email],[harrisonrj@ornl.gov])

# Process this file with autoconf to produce a configure script.
define([AC_CACHE_LOAD], )dnl for debugging configure.in
define([AC_CACHE_SAVE], )dnl for debugging configure.in
AC_PREREQ(2.59)
AC_INIT([madness],[madness_mmm_version],[madness_email])
AC_CONFIG_SRCDIR(src/lib/tensor/tensor.h)
AC_CONFIG_HEADER(include/madness_config.h)
AC_PROG_MAKE_SET([])
# That's where extra scripts necessary for configuration (config.guess, etc.) are located
AC_CONFIG_AUX_DIR(bin)

dnl --- Compiling C++ 99% of the time ---
AC_LANG(C++)

AC_CANONICAL_TARGET
AC_DEFINE_UNQUOTED(HOST_ARCH, "$host")
AC_DEFINE_UNQUOTED(TARGET_ARCH, "$target")

# Set up the default paths.
AC_PREFIX_DEFAULT(/usr/local/madness)

# Disect version info
MADNESS_MMM_VERSION=madness_mmm_version
MADNESS_BUILDID=madness_buildid
AC_SUBST(MADNESS_MMM_VERSION)
AC_SUBST(MADNESS_BUILDID)
changequote(<<, >>)dnl
MADNESS_MAJOR_VERSION=`echo $MADNESS_MMM_VERSION|sed 's/\([0-9]*\)\.[0-9]*\.[0-9]*/\1/'`
MADNESS_MINOR_VERSION=`echo $MADNESS_MMM_VERSION|sed 's/[0-9]*\.\([0-9]*\)\.[0-9]*/\1/'`
MADNESS_MICRO_VERSION=`echo $MADNESS_MMM_VERSION|sed 's/[0-9]*\.[0-9]*\.\([0-9]*\)/\1/'`
changequote([, ])dnl
AC_DEFINE_UNQUOTED(MADNESS_MAJOR_VERSION,$MADNESS_MAJOR_VERSION)
AC_DEFINE_UNQUOTED(MADNESS_MINOR_VERSION,$MADNESS_MINOR_VERSION)
AC_DEFINE_UNQUOTED(MADNESS_MICRO_VERSION,$MADNESS_MICRO_VERSION)
AC_SUBST(MADNESS_MAJOR_VERSION)
AC_SUBST(MADNESS_MINOR_VERSION)
AC_SUBST(MADNESS_MICRO_VERSION)
MADNESS_SO_VERSION=madness_so_version
AC_SUBST(MADNESS_SO_VERSION)

dnl --------- Features ---------

AC_ARG_ENABLE(debug,
[  --enable-debug          Compile with debugging options],
[
case $enableval in
  yes)
    DEBUG=yes
  ;;
  no)
    DEBUG=no
  ;;
  opt)
    DEBUG=opt
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --enable-debug ($enableval)])
  ;;
esac
],[
    DEBUG=no
]
)

madincludedir=$includedir
AC_ARG_WITH(mad-includedir,
[  --with-mad-includedir   Specifies include file install subdir.],
madincludedir=$withval
)
AC_SUBST(madincludedir)

# In the future may check if some directories to be excluded -- for now compile all
EXCLUDED_DIRS=

BUILDID="madness_buildid"
AC_ARG_WITH(build-id,
[  --with-build-id         Gives an identifier for the build.],
BUILDID=$withval
)
AC_SUBST(BUILDID)
AC_DEFINE_UNQUOTED(MADNESS_BUILDID,"$BUILDID")

if test "$BUILDID"; then
  MADNESS_VERSION="$MADNESS_MMM_VERSION-$BUILDID"
else
  MADNESS_VERSION="$MADNESS_MMM_VERSION"
fi

AC_DEFINE_UNQUOTED(MADNESS_VERSION, "$MADNESS_VERSION")
AC_SUBST(MADNESS_VERSION)


dnl --------- Compilers and Tools ---------

AC_ARG_WITH(cxx,
[  --with-cxx              Gives the name of the C++ compiler to use.],
CXX=$withval
)

CXXOPTIONS_OPT=
AC_ARG_WITH(cxx-optflags,
[  --with-cxx-optflags     Gives optimization flags for the C++ compiler.],
CXXOPTIONS_OPT=$withval
)

AC_ARG_WITH(cc,
[  --with-cc               Gives the name of the C compiler to use.],
CC=$withval
)

CCOPTIONS_OPT=
AC_ARG_WITH(cc-optflags,
[  --with-cc-optflags      Gives optimization flags for the C compiler.],
CCOPTIONS_OPT=$withval
)

AC_ARG_WITH(ranlib,
[  --with-ranlib           Gives the name of the ranlib program.],
RANLIB=$withval
)

AC_ARG_WITH(ar,
[  --with-ar               Names the archive creator.],
AR=$withval
)

ARFLAGS=r
AC_ARG_WITH(ar-flags,
[  --with-ar-flags         Flags for the the archive creator.],
ARFLAGS=$withval
)
AC_SUBST(ARFLAGS)

AC_ARG_WITH(ld,
[  --with-ld               Names the object linker.],
LD=$withval
)

DOT_PATH=yes
AC_ARG_WITH(dot,
[  --with-dot              Gives the path to the dot graph generator.],
DOT_PATH=$withval
)

AC_ARG_WITH(incdirs,
[  --with-incdirs          Specifies include directories (-Idir1 -Idir2).],
EXTRAINCDIRS=$withval
CPPFLAGS=$withval
echo Using extra include directories: $withval
)

template_instantiation=none
AC_ARG_WITH(template,
[  --with-template         Specifies template instantation model.],
template_instantiation=$withval
echo Using template instatiation model: $withval
)

AC_ARG_WITH(libs,
[  --with-libs             Specifies libraries (-llib1 -llib2).],
LIBS=$withval
echo Using extra libraries: $withval
)

LDFLAGS=
LIBDIRS=
AC_ARG_WITH(libdirs,
[  --with-libdirs          Specifies library directories (-Ldir1 -Ldir2).],
LIBDIRS=$withval
LDFLAGS=$withval
echo Using extra library directories: $withval
)

dnl --------- Want absolute path for srcdir, not relative. ---------

if test X$ac_srcdir_defaulted = Xyes; then
  case $srcdir in
    ../*)
      srcdir=`(cd $srcdir; pwd)`
      #srcdir=`(cd $srcdir; echo $PWD)`
      ;;
  esac
fi

dnl --------- Checks for programs. ---------
AC_PROG_MAKE_SET
AC_PROG_LN_S
AC_PROG_INSTALL
AC_PROG_RANLIB
dnl ac_prog_cxx's order isn't what i need
AC_CHECK_PROGS(CXX, g++ c++ gcc CC cxx xlC_r, g++)
dnl sees if CXX is a GNU compiler
AC_PROG_CXX
dnl find C compiler and check if it is a GNU compiler
AC_PROG_CC

AC_PROG_CXXCPP
AC_PROG_CPP
AC_CHECK_PROG(AR,ar,ar)
AC_CHECK_PROG(PERL,perl,perl)
AC_CHECK_PROG(DOXYGEN,doxygen,doxygen,NO)
if test X$LD = X; then
  LD=$CXX
  AC_SUBST(LD)
fi

dnl The lack of certain tools is a show stopper

changequote(<<, >>)dnl

if [ X$PERL = X ]; then
  echo "Could not find the program perl!"
  echo "Most system vendors provide it."
  echo "It can also be obtained at"
  echo "ftp://prep.ai.mit.edu/pub/gnu"
  exit 1
fi

changequote([, ])dnl


dnl --------- Check for dot -----------

if test X$DOT_PATH = Xno; then
  HAVE_DOT=NO
else
  AC_PATH_PROG(DOT,dot,,$DOT_PATH:$PATH:/usr/local/bin)
  DOT_PATH=`AS_DIRNAME(["$DOT"])`
  if test X$DOT_PATH = X; then
    HAVE_DOT=NO
  else
    HAVE_DOT=YES
  fi
fi
AC_SUBST(DOT_PATH)
AC_SUBST(HAVE_DOT)

dnl --------- Check for PDFLaTeX -----------

AC_PATH_PROG(PDFLATEX,pdflatex,NO,$PATH:/usr/local/bin)
HAVE_PDFLATEX=YES
if test X"$PDFLATEX" = XNO; then
  HAVE_PDFLATEX=NO
fi
AC_SUBST(HAVE_PDFLATEX)

dnl -------- Checks for compiler/linker options. ----------

if test "X$CXXOPTIONS_OPT" = X; then
  AC_MSG_WARN([C++ optimization options are not given! For optimum performance use --with-cxx-optflags configure option])
fi

# options needed only for optimization
if test "X$CXXOPTIONS_OPT" = X; then
  CXXOPTIONS_OPT=-O2
fi
if test "X$CCOPTIONS_OPT" = X; then
  CCOPTIONS_OPT=-O2
fi
# options needed only for debugging
CXXOPTIONS_DBG=-g
CCOPTIONS_DBG=-g
# options that are always needed
CXXOPTIONS_MISC=
CCOPTIONS_MISC=

OBJSUF=o
LIBSUF=a

dnl -- check how dependency information is built --

# The GNU compilers work with:
CXXDEPENDSUF=none
CXXDEPENDFLAGS=-M
CCDEPENDSUF=none
CCDEPENDFLAGS=-M

/bin/rm -f depcheck.u depcheck.cc depcheck.c depcheck.o

# Check for an IBM visual age C++ compiler
echo "#include <iostream>" > depcheck.cc
$CXX $CPPFLAGS $CXXFLAGS -M -E depcheck.cc > /dev/null 2>&1
if test -f depcheck.u; then
  CXXDEPENDSUF=u
  CXXDEPENDFLAGS="-M -E"
fi
/bin/rm -f depcheck.u depcheck.cc depcheck.o

# Check for an IBM visual age C compiler
echo "#include <stdio.h>" > depcheck.c
$CC $CPPFLAGS $CFLAGS -M -E depcheck.c > /dev/null 2>&1
if test -f depcheck.u; then
  CCDEPENDSUF=u
  CCDEPENDFLAGS="-M -E"
fi
/bin/rm -f depcheck.u depcheck.c depcheck.o

dnl -- special optimization options --

AC_MSG_CHECKING([for additional optimization options])
case $target in
  i686-pc-linux-*)
    if test X$GXX != Xyes; then
      CXXOPTIONS_OPT="$CXXOPTIONS_OPT -march=pentium4 -mtune=pentium4 -msse2 -mfpmath=sse"
    fi
    if test X$GCC != Xyes; then
      CCOPTIONS_OPT="$CCOPTIONS_OPT -march=pentium4 -mtune=pentium4 -msse2 -mfpmath=sse"
    fi
    AC_MSG_RESULT([pentium4])
  ;;
  *)
    AC_MSG_RESULT([none])
  ;;
esac

dnl ----------- check for GNU libc++-v3 prerelease bug ---------------

if test X$GXX = Xyes; then
AC_MSG_CHECKING([for GNU libc++-v3 prerelease bug])
AC_LINK_IFELSE([AC_LANG_SOURCE(
[[#include <stdio.h>
#include <$iostream>
]],[[
]])],[
AC_MSG_RESULT([no])
],
EXTRADEFINES="$EXTRADEFINES -D_ISOC99_SOURCE=1"
AC_MSG_RESULT([yes])
);
fi

dnl ------------ check compiler peculiarities -------------

AC_MSG_CHECKING([GNU c++ library/template flags])
if test X$GXX = Xyes; then
  # automatic template instantiation causes problems with libstc++-v3
  #CXXOPTIONS_MISC="$CXXOPTIONS_MISC -fno-implicit-templates"
  #EXPLICIT_TEMPLATE_INSTANTIATION=yes
  #AC_DEFINE(EXPLICIT_TEMPLATE_INSTANTIATION)
  AC_MSG_RESULT([GNU])
else
  case $target in
    *)
      AC_MSG_RESULT([generic non GNU])
      ;;
    esac
fi

dnl -- if unqualified static declarations are considered -- 
dnl -- XL C++ version 6 does not --
AC_MSG_CHECKING([if unqualified static declarations are considered])
AC_COMPILE_IFELSE(
  [AC_LANG_PROGRAM(
    [[template <typename T>
      static inline void f(T* a) {};
      template <typename T> void g(T* a) { f(a); }
      template void g(int* a);]],
    [[]])],
  [AC_DEFINE(HAVE_UNQUALIFIED_STATIC_DECL) AC_MSG_RESULT([yes])],
  [AC_MSG_RESULT([no])]
)

dnl -- IBM XL C++ version 6 is severely broken with regard to nested
dnl -- template processing. This example breaks the compiler.
AC_MSG_CHECKING([for nested template xlC bug])
AC_COMPILE_IFELSE(
  [AC_LANG_PROGRAM(
    [[template <typename T> struct Traits {
        enum {FeatureSupported = false};
      };
      template <> struct Traits<int> {
        enum {FeatureSupported = true};
      };
      // TypeSelector by default returns double, unless Tr supports Feature
      template <typename Tr, typename T, bool supp = Tr::FeatureSupported>
        struct TypeSelector {
          typedef double result;
        };
      template <typename Tr, typename T>
        struct TypeSelector<Tr,T,true> {
          typedef T result;
        };

      template <typename T> typename TypeSelector<Traits<T>,double>::result f(T a) {
        return a;
      }
      // Including this overload changes error completely!
      double f(int a);
      void g() {
        double x = f<int>(1);
      }
    ]],
    [[]])],
  [AC_MSG_RESULT([no])],
  [AC_DEFINE(HAVE_NESTED_TEMPLATE_XLC_BUG) AC_MSG_RESULT([yes])]
)

dnl -- Check if template function instantiation can be used as a class template argument
dnl -- IBM XL C++ version 6 doesn't support this
AC_MSG_CHECKING([if template function instantiation can be used for template argument deduction])
AC_COMPILE_IFELSE(
  [AC_LANG_PROGRAM(
    [[template <typename T, class Op>
        void SuperOp(T* data, const Op op) {
          op(data);
        }

      template <typename T>
        void zero(T* data) {
          data[0] = 0;
        }
      void dzero(double* data) { data[0] = 0; }

      void f() {
        double* data = new double[1];
        SuperOp(data,dzero);              // OK
        SuperOp(data,zero<double>);       // Fails?
      }
    ]],
    [[]])],
  [AC_DEFINE(HAVE_TFI_FOR_TEMPLATE_ARGUMENT_DEDUCTION) AC_MSG_RESULT([yes])],
  [AC_MSG_RESULT([no])]
)



if test $DEBUG = yes; then
  CXXFLAGS="$CXXOPTIONS_DBG $CXXOPTIONS_MISC"
  CFLAGS="$CCOPTIONS_DBG $CCOPTIONS_MISC"
  LDFLAGS="$LDFLAGS -g"
elif test $DEBUG = opt; then
  CXXFLAGS="$CXXOPTIONS_DBG $CXXOPTIONS_OPT $CXXOPTIONS_MISC"
  CFLAGS="$CCOPTIONS_DBG $CCOPTIONS_OPT $CCOPTIONS_MISC"
  LDFLAGS="$LDFLAGS -g"
else
  CXXFLAGS="$CXXOPTIONS_OPT $CXXOPTIONS_MISC"
  CFLAGS="$CCOPTIONS_OPT $CCOPTIONS_MISC"
fi

AC_SUBST(EXTRAINCLUDE)
AC_SUBST(EXTRADEFINES)
AC_SUBST(CXXFLAGS)
AC_SUBST(CFLAGS)
AC_SUBST(LIBDIRS)
AC_SUBST(OBJSUF)
AC_SUBST(LIBSUF)
AC_SUBST(CXXDEPENDSUF)
AC_SUBST(CXXDEPENDFLAGS)
AC_SUBST(CCDEPENDSUF)
AC_SUBST(CCDEPENDFLAGS)

dnl -------- Checks for architecture specific features. --------
dnl (Doesn't work for cross compilation.)
dnl AC_CHECK_SIZEOF(void*)

dnl ---------- Checks for header files. -----------

AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h stdint.h)

dnl -- Checks for typedefs, structures, and compiler characteristics. --

AC_TYPE_SIZE_T

AC_C_RESTRICT
EXTRADEFINES="$EXTRADEFINES -DRESTRICT=restrict"

dnl ---------- Checks for C++/C library functions -----------

dnl -- check for std::abs(long) --
AC_MSG_CHECKING([std::abs(long)])
AC_LINK_IFELSE([AC_LANG_PROGRAM([[#include <cmath>
                                  #include <cstdlib>]],
                                [[long (*absptr)(long) = &std::abs; long a = -1l;  long b = std::abs(a);]])],
               [AC_DEFINE(HAVE_STD_ABS_LONG) have_abs_long=yes],
               [have_abs_long=no])
AC_MSG_RESULT([$have_abs_long])

dnl -- alternatively, check for std::labs(long) --
if test X"$have_abs_long" = Xno; then
  AC_MSG_CHECKING([std::labs(long)])
  AC_LINK_IFELSE([AC_LANG_PROGRAM([[#include <cmath>
                                    #include <cstdlib>]],
                                  [[long (*labsptr)(long) = &std::labs; long a = -1l;  long b = labs(a);]])],
                 [AC_DEFINE(HAVE_LABS) have_std_labs=yes],
                 [have_std_labs=no])
  AC_MSG_RESULT([$have_std_labs])
fi

dnl ---------- Checks for Boost library. -----------

AC_CHECK_HEADERS(boost/shared_array.hpp)
AC_MSG_CHECKING([if boost::shared_array<double> is usable])
AC_LINK_IFELSE([AC_LANG_PROGRAM(
[[#include <boost/shared_array.hpp>]],
[[boost::shared_array<double> x(new double[1]); boost::shared_array<double> y(x);]])],
[AC_DEFINE(HAVE_BOOST_SHARED_ARRAY_COMPILED)
 AC_MSG_RESULT([yes])],
[AC_MSG_RESULT([no])]
)


dnl -----------------------------------
dnl ---------- Misc checks. -----------
dnl -----------------------------------

dnl ----------- Fortran symbol names --------------
dnl No checking is done, expert user is assumed

F77_SYMBOL="symbol_"
AC_ARG_WITH(f77symbol,[  --with-f77symbol        FORTRAN77 symbol convention. Allowed values are:
                            lc  (lower-case)
                            lcu (lower-case with underscore; default, if not given)
                            uc  (upper-case)
                            ucu (upper-case with underscore)],[
case $withval in
  lc)
    F77_SYMBOL="symbol"
    ;;
  lcu)
    F77_SYMBOL="symbol_"
    ;;
  uc)
    F77_SYMBOL="SYMBOL"
    ;;
  ucu)
    F77_SYMBOL="SYMBOL_"
    ;;
  *)
    AC_MSG_WARN([did not recognize value $withval. will assume lower-case with underscore])
    ;;
esac
])
AC_SUBST(F77_SYMBOL)

dnl ----------- BLAS and LAPACK libraries --------------

AC_ARG_WITH(blas,[  --with-blas             which blas library to use],[BLAS=$withval])

if test X$BLAS = X; then
  DGEMM_SYMBOL=`$PERL $srcdir/bin/mkf77sym.pl $F77_SYMBOL DGEMM`

  dnl it is possible blas was fiven via --with-libs
  AC_CHECK_FUNC($DGEMM_SYMBOL,
    BLAS_IN_LIBS=yes)
  
  if test X$BLAS_IN_LIBS = X; then
    if test "X$BLAS" = X; then
    AC_CHECK_LIB(essl, $DGEMM_SYMBOL, BLAS="-lessl")
    fi
    if test "X$BLAS" = X; then
    AC_CHECK_LIB(scs, $DGEMM_SYMBOL, BLAS="-lscs")
    fi
    if test "X$BLAS" = X; then
    AC_CHECK_LIB(atlas, $DGEMM_SYMBOL, BLAS="-latlas")
    fi
    if test "X$BLAS" = X; then
    AC_CHECK_LIB(blas, $DGEMM_SYMBOL, BLAS="-lblas")
    fi
    if test "X$BLAS" = X; then
    AC_MSG_ERROR("Did not find a BLAS library. Make sure you ")
    fi
    AC_MSG_RESULT([BLAS library is $BLAS])
  else
    AC_MSG_RESULT([BLAS library is included in LIBS])
  fi
fi
AC_SUBST(BLAS)

AC_ARG_WITH(lapack,[  --with-lapack           which lapack library to use],[LAPACK=" $withval"])

dnl look for dgesvd in LAPACK
DGESVD_SYMBOL=`$PERL $srcdir/bin/mkf77sym.pl $F77_SYMBOL DGESVD`

dnl first test if BLAS-containing library contains LAPACK as well
if test "X$LAPACK" = X; then
  CURRENT_LIBS=$LIBS
  LIBS="$BLAS $LIBS"
  AC_CHECK_FUNC($DGESVD_SYMBOL,LAPACK_IN_BLAS=yes)
  LIBS=$CURRENT_LIBS
fi
if test X$LAPACK_IN_BLAS = X; then
  if test "X$LAPACK" = X; then
    AC_CHECK_LIB(lapack,$DGESVD_SYMBOL, LAPACK="-llapack",,$BLAS)
  fi
  if test "X$LAPACK" = X; then
    AC_MSG_ERROR("Did not find a LAPACK library")
  fi
  AC_MSG_RESULT([LAPACK library is $LAPACK])
else
  AC_MSG_RESULT([LAPACK library is included in BLAS+LIBS])
fi
AC_SUBST(LAPACK)

dnl ----------- MPI and Pthreads libraries --------------

enableparallel=yes
AC_ARG_ENABLE(parallel,
  [  --disable-parallel      disable parallel (MPI,threads) features],
  [
case $enableval in
  yes)
  ;;
  no)
    enableparallel=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --(dis|en)able-parallel ($enableval)])
  ;;
esac
])

if test X"$enableparallel" = Xyes; then
  
  dnl ---- look for MPI ----
  AC_CHECK_HEADERS(mpi.h,HAVE_MPI_H=yes)
  dnl -- sometimes mpi.h breaks C++ compiler -- try using C compiler here --
  if test X"$HAVE_MPI_H" = X; then
    AC_LANG_PUSH(C)
    AC_CHECK_HEADERS(mpi.h,[HAVE_MPI_H=yes
                            MPI_H_BREAKS_CXX=yes
                            AC_DEFINE(MPI_H_BREAKS_CXX)
                            AC_SUBST(MPI_H_BREAKS_CXX)])
    AC_LANG_POP(C)
  fi
  
  if test X"$HAVE_MPI_H" = Xyes; then
    MPI_INIT_SYMBOL=`$PERL $srcdir/bin/mkf77sym.pl $F77_SYMBOL MPI_INIT`
    dnl AC_SEARCH_LIB doesn't seem to work yet
    AC_CHECK_FUNC($MPI_INIT_SYMBOL,[HAVE_MPI_LIB=yes HAVE_MPI_F77_BINDINGS=yes])
    if test X"$HAVE_MPI_LIB" = X; then
      AC_CHECK_LIB(mpi,$MPI_INIT_SYMBOL,LIBS="-lmpi $LIBS" HAVE_MPI_LIB=yes)
    fi
    if test X"$HAVE_MPI_LIB" = X; then
      AC_CHECK_LIB(mpich,$MPI_INIT_SYMBOL,LIBS="-lmpich $LIBS" HAVE_MPI_LIB=yes)
    fi
    if test X"$HAVE_MPI_LIB" = Xyes; then
      AC_DEFINE(HAVE_MPI_LIB)
    fi

    dnl -- Test for C MPI bindings --
    AC_LANG_PUSH(C)
    AC_MSG_CHECKING([for MPI C bindings])
    AC_LINK_IFELSE(
      [AC_LANG_PROGRAM(
        [[#include <mpi.h>]],
        [[int argc = 0; char** argv = 0; MPI_Init(&argc,&argv); MPI_Finalize();]]
       )
      ],
      [AC_DEFINE(HAVE_MPI_C_BINDINGS)
       HAVE_MPI_C_BINDINGS=yes
       AC_MSG_RESULT([yes])
      ],
      [AC_MSG_RESULT([no])]
    )
    AC_LANG_POP(C)

    dnl -- Test for C++ MPI bindings --
    AC_MSG_CHECKING([for MPI C++ bindings])
    AC_LINK_IFELSE(
      [AC_LANG_PROGRAM(
        [[#include <mpi.h>]],
        [[int argc = 0; char** argv = 0; MPI::Init(argc,argv); MPI::Finalize();]]
       )
      ],
      [AC_DEFINE(HAVE_MPI_CXX_BINDINGS)
       HAVE_MPI_CXX_BINDINGS=yes
       AC_MSG_RESULT([yes])
      ],
      [AC_MSG_RESULT([no])]
    )
    
    dnl -- now can decide whether have enough MPI support to compile --
    if test X"$HAVE_MPI_H""$HAVE_MPI_LIB" = Xyesyes; then
      if test X"$HAVE_MPI_C_BINDINGS" = Xyes -o X"$HAVE_MPI_CXX_BINDINGS" = Xyes; then
        HAVE_MPI=yes
        AC_SUBST(HAVE_MPI)
        AC_SUBST(HAVE_MPI_CXX_BINDINGS)
      fi
    fi
    
  fi
  
  dnl ==== MPI is required! ====
  if test X"$HAVE_MPI" = X; then
    AC_MSG_ERROR([MPI library is not found. Cannot proceed.])
  fi
  
  dnl ---- look for pthreads ----
  AC_CHECK_HEADERS(pthread.h,HAVE_PTHREAD_H=yes)
  if test X"$HAVE_PTHREAD_H" = Xyes; then
    PTHREADS_SYMBOL=pthread_join
    dnl AC_SEARCH_LIB doesn't seem to work yet
    AC_CHECK_FUNC($PTHREADS_SYMBOL,HAVE_PTHREAD_LIB=yes)
    if test X"$HAVE_PTHREAD_LIB" = X; then
      AC_CHECK_LIB(pthread,$PTHREADS_SYMBOL,LIBS="$LIBS -lpthread" HAVE_PTHREAD_LIB=yes)
    fi
    if test X"$HAVE_PTHREAD_LIB" = X; then
      AC_CHECK_LIB(pthreads,$PTHREADS_SYMBOL,LIBS="$LIBS -lpthreads" HAVE_PTHREAD_LIB=yes)
    fi
    if test X"$HAVE_PTHREAD_LIB" = Xyes; then
      AC_DEFINE(HAVE_PTHREAD_LIB)
    fi
    if test X"$HAVE_PTHREAD_H""$HAVE_PTHREAD_LIB" = Xyesyes; then
      HAVE_PTHREADS=yes
      AC_SUBST(HAVE_PTHREADS)
    fi
  fi
  
fi

dnl --------- Template instantiation. ---------

if test $template_instantiation = auto; then
AC_MSG_CHECKING(choosing template instantiation method)
  if test X$GXX = Xyes; then
    AC_MSG_RESULT("gcc (none)")
  else
    case $target in
      *-sgi-irix6.0*)
        AC_MSG_RESULT("*-sgi-irix6.0* (none)")
        ;;
      *-sgi-irix*)
        template_instantiation=ptused
        AC_MSG_RESULT("*-sgi-irix* (ptused)")
        ;;
      *-cray-unicos*)
        template_instantiation=craysimp
        AC_MSG_RESULT("*-cray-unicos* (craysimp)")
        ;;
      *-dec-osf*)
        template_instantiation=repodir
        AC_MSG_RESULT("*-dec-osf* (repodir)")
        ;;
      alpha*)
        template_instantiation=repodir
        AC_MSG_RESULT("alpha* non-GNU C++ (repodir)")
        ;;
      *-solaris*)
        template_instantiation=sunexpl
        AC_MSG_RESULT("*-solaris* (sunexpl)")
        ;;
      *)
        AC_MSG_RESULT("generic non GNU (none)")
        ;;
    esac
  fi
fi

if test $template_instantiation = auto; then
  template_instantiation=none
fi

AC_MSG_CHECKING(template instantiation flags)
TMPLINST=no
TMPLREPO=
TMPLINLIB=no
case $template_instantiation in
  repodir)
    TMPLREPO=cxx_repository
    TMPLINLIB=yes
    ;;
  craysimp)
    CXXFLAGS="$CXXFLAGS -h simple_templates"
    ;;
  gnurepo)
    TMPLINST=yes
    CXXFLAGS="$CXXFLAGS -frepo"
    AC_MSG_RESULT(gnurepo)
    ;;
  gnuexpl)
    # automatic template instantiation causes problems with libstc++-v3
    CXXFLAGS="$CXXFLAGS -fno-implicit-templates"
    EXPLICIT_TEMPLATE_INSTANTIATION=yes
    AC_DEFINE(EXPLICIT_TEMPLATE_INSTANTIATION)
    ;;
  sunexpl)
    CXXFLAGS="$CXXFLAGS -instances=explicit"
    AC_DEFINE(EXPLICIT_TEMPLATE_INSTANTIATION)
    ;;
  ptused)
    CXXFLAGS="$CXXFLAGS -ptused"
    ;;
  none)
    AC_MSG_RESULT(none)
    ;;
  *)
    AC_MSG_RESULT($template_instantiation)
    AC_MSG_ERROR(unknown template instantiation method)
esac

AC_SUBST(TMPLINST)
AC_SUBST(TMPLREPO)
AC_SUBST(TMPLINLIB)


dnl -------------------------------------------------
dnl --------- Shared library configuration. ---------
dnl -------------------------------------------------
enablelibtool=no
AC_ARG_ENABLE(libtool,
[[  --enable-libtool        Use libtool [default=no] ]],
[
case $enableval in
  yes)
    enablelibtool=yes
  ;;
  no)
    enablelibtool=no
  ;;
  *)
    AC_MSG_ERROR([Invalid value for --(dis|en)able-libtool ($enableval)])
  ;;
esac
])

if test "$enablelibtool" = yes; then

dnl by default make only static libraries
AC_DISABLE_SHARED
AC_PROG_LIBTOOL

OBJSUF=lo
LIBSUF=la

else

enable_shared=no

fi

if test "$enable_shared" = "no"; then
  OBJSUF=o
  LIBSUF=a
fi

ENABLESHARED=$enable_shared
AC_SUBST(ENABLESHARED)


dnl --------- prepare to retire: write the results, etc. ---------

AC_CONFIG_FILES(lib/MakeVars doc/doxygen.cfg)

dnl make Makefiles in object tree
AC_CONFIG_COMMANDS([object_tree_Makefiles],
                   [$PERL $fullsrcdir/bin/objectdir.pl $EXCLUDED_DIRS $fullsrcdir],
                   [PERL=$PERL EXCLUDED_DIRS=$EXCLUDED_DIRS fullsrcdir=$srcdir])

dnl process f77 symbols
AC_CONFIG_COMMANDS([src/lib/linalg/lapack_functions.h],
                   [$PERL $fullsrcdir/bin/mkf77sym.pl $F77_SYMBOL <$fullsrcdir/src/lib/linalg/lapack_functions.h.in >src/lib/linalg/lapack_functions.h],
                   [PERL=$PERL F77_SYMBOL=$F77_SYMBOL fullsrcdir=$srcdir])

dnl escape important directory names
prefix=`echo $prefix | sed 's/ /\\ /g'`

dnl --------- Done! ---------
AC_OUTPUT

